#!/usr/bin/env perl6
use v6;

constant PULP = $*DISTRO.is-win ?? "pulp.cmd" !! "pulp";
constant PERL6 = $*DISTRO.is-win ?? "perl6.bat" !! "perl6";
constant PURSPGPP = "bower_components/purescript-postgresql-client/purspgpp";
constant SCSS = $*DISTRO.is-win ?? "scss.bat" !! "scss";

sub purspgpp(Str:D $connection-string) {
    use fatal;
    for
        ".gitignore".IO.lines
        .grep({"# BEGIN PURSPGPP OUTPUT" ^ff^ "# END PURSPGPP OUTPUT"})
        .map(($*CWD ~ *).IO)
    -> $out {
        my $in = $out.subst(/'.purs'$/, ".purspg").IO;
        run PERL6, PURSPGPP, $connection-string, $in, $out;
    }
}

sub browserify {
    use fatal;
    mkdir "output";
    my $out-js = open("output/nn.js", :w, :bin);
    run PULP, "browserify", "--main", "Main.Client", :out($out-js);
}

sub scss {
    use fatal;
    mkdir "output";
    run SCSS, "css/main.scss", "output/nn.css";
}

sub MAIN {
    my $connection-string = %*ENV<NN_BUILD_CONNECTION_STRING>;
    purspgpp $connection-string;
    browserify;
    scss;
}
